options
{
 LOOKAHEAD=1;
 MULTI = true;
 static = false;
 VISITOR = true;
}

PARSER_BEGIN(YAL2JVM)
import java.io.* ;
// código Java que invoca o parser
public class YAL2JVM {

public static int errors = 0; // use this variable in order to control the maximum of errors (10)
public int line = 0;
public int column = 0;

 public static void main(String args[]) throws ParseException {
// criação do objecto utilizando o constructor com argumento para
// ler do standard input (teclado)
 YAL2JVMTokenManager tm;
  if (args.length<1) {
        System.out.println("Ficheiro não especificado!");
        System.exit(1);
  }
  try{
      java.io.InputStream infile;
      infile = new java.io.FileInputStream(args[0]);

      tm = new   YAL2JVMTokenManager(new SimpleCharStream(infile));
      System.out.println("Ficheiro lido com sucesso!");

      YAL2JVM parser = new YAL2JVM(infile);
      ASTModule module = parser.Module();
      module.dump("");
      System.out.println("Leitura terminada.");
      if(errors>0){
        System.out.println("Fix Errors!!!");
        System.exit(0);
      }
      if(!module.analyse(null)){
        System.out.println("Warning!!! You have errors and you should fix them\n Generated Code may be incorrect!!!");
        System.exit(0);
      }
      //Code to Traverse Tree
      PostorderVisitor v = new PostorderVisitor();
      module.jjtAccept(v,null);
      v.reverseStack();

        System.out.println("----------");
      System.out.println();


      for(int i = 0; i < v.getStack().size(); i++){
          System.out.println(v.getStack().get(i).getType()+": "+v.getStack().get(i).getName());
        }

      System.out.println("----------");
      System.out.println();

      GenByteCode progj = new GenByteCode(v.getStack());

      System.out.println("----------");
      System.out.println();

      progj.printIR();

      System.out.println("----------");
      System.out.println();

      progj.printLines();
      String filename = args[0];

      int lastSeparatorIndex = filename.lastIndexOf("/");
      if (lastSeparatorIndex == -1) {
        lastSeparatorIndex = filename.lastIndexOf("\\");
        if (lastSeparatorIndex == -1) {
          filename = filename;
        } else {
            filename = filename.substring(lastSeparatorIndex + 1);
        }
      } else {
          filename = filename.substring(lastSeparatorIndex + 1);
      }

      int extensionIndex = filename.lastIndexOf(".");

      filename = filename.substring(0, extensionIndex);
      filename += ".j";
      progj.writeToFile(filename);

  }
  catch (java.io.FileNotFoundException e) {
      System.out.println("Ficheiro "+args[0]+" não encontrado");
      return;
    }

 }

}
PARSER_END(YAL2JVM)


JAVACODE
void skipt_do_not_consume_last(ParseException e, String expr, int kind) {


  if(errors == 0) {
    System.out.println("--->Exception Catched!!! The message: <---");
    System.out.println();
  }

  errors++;
  System.out.println();
  System.out.println("yal2jvm.jj:error: Something went wrong on " + expr);
  System.out.println(e.currentToken.next.image + " <-- Line:" + e.currentToken.beginLine + ", column:" + e.currentToken.next.beginColumn);
  System.out.println("Expecting: ");
  for(int i = 0; i < e.expectedTokenSequences.length; i++) {
    for(int j = 0; j < e.expectedTokenSequences[i].length; j++) {
      System.out.println(tokenImage[e.expectedTokenSequences[i][j]]);
    }
  }
  System.out.println();

  System.out.println("Number Errors: " +errors);

  int token_kind;
  Token t;
  boolean canI = false;
  do {

    if(canI) {
      t = getNextToken();
    }
    token_kind = jj_ntk_f();
    canI = true;
  } while (token_kind != kind);
}
JAVACODE
void error_control(ParseException e, String expr, int skipKind) {

    ParseException p = generateParseException();
    if(errors == 0) {
      System.out.println("--->Exception Catched!!! The message: <---");
      System.out.println();
    }

    errors++;
    System.out.println();
    System.out.println("yal2jvm.jj:error: Something went wrong on!!!");
    System.out.println(p.currentToken.next.image + " <-- Line:" + p.currentToken.beginLine + ", column:" + p.currentToken.next.beginColumn);
    System.out.println("Expecting: ");
    for(int i = 0; i < p.expectedTokenSequences.length; i++) {
      for(int j = 0; j < p.expectedTokenSequences[i].length; j++) {
        System.out.println(tokenImage[p.expectedTokenSequences[i][j]]);
      }
    }
    System.out.println();

    System.out.println("Number Errors: " +errors);

    if(errors > 10) {
      System.out.println("You have at least 10 errors!!!");
      System.exit(1);
    }


    Token t;
    do {
      t = getNextToken();

    } while (t.kind != skipKind);


}

// símbolos que não devem ser considerados na análise
SKIP :
{
  " "
  | "\t"
  | "\n"
  | "\r"
  | <"//" (~["\n","\r"])* ("\n" | "\r" | "\r\n")>
  | <"/*" (~["*"])* "*" ("*" | ~["*","/"] (~["*"])* "*")* "/">
}
// definição dos tokens (símbolos terminais)
/* reserved words */
<DEFAULT> TOKEN : {
<RELA_OP: ">" | "<" | "<=" | ">=" | "==" | "!=">
| <ADDSUB_OP: "+" | "-">
| <ARITH_OP: "*" | "/" | "<<" | ">>" | ">>>">
| <BITWISE_OP: "&" | "|" | "^">
| <NOT_OP: "!">
| <WHILE: "while">
| <IF: "if">
| <ELSE: "else">
| <ASSIGN: "=">
| <ASPA: "\"">
| <LPAR: "(">
| <RPAR: ")">
| <VIRG: ",">
| <PVIRG: ";">
| <LCHAVETA: "{">
| <RCHAVETA: "}">
| <FUNCTION: "function">
| <MODULE: "module">
| <SIZE: "size">
}

<DEFAULT> TOKEN : {
<INTEGER: (<DIGIT>)+>
| <ID: <LETTER> (<LETTER> | <DIGIT>)*>
| <#LETTER: ["$","A"-"Z","_","a"-"z"]>
| <#DIGIT: ["0"-"9"]>
| <STRING: "\"" (["a"-"z","A"-"Z","0"-"9",":"," ","="])+ "\"">
}

// definição da produção

ASTModule Module() : {Token t;}
{

  <MODULE> t=<ID>{jjtThis.setName(t.image);} <LCHAVETA>  (LOOKAHEAD(2) Declaration() )*
  try {
    ( Function() )*  <RCHAVETA>
  } catch(ParseException e) {
    error_control(e, "Module", RCHAVETA);
  }
  {return jjtThis;}
}

void Declaration() : {Token num; Token op;}
{
  try {
  (LOOKAHEAD(<ID>"[""]") ArrayElement() | ScalarElement() ) ( <ASSIGN> ( ( "[" ArraySize() "]" ) | (
  op=<ADDSUB_OP>{jjtThis.setOp(op.image);} )? num=<INTEGER>{jjtThis.setNumber(num.image);} ) )? <PVIRG>
  } catch(ParseException e) {
    error_control(e, "Declaration", PVIRG);
  }
}

void Function() : {Token t;}
{
    try {
    <FUNCTION>
    ( LOOKAHEAD(2)( (LOOKAHEAD(<ID>"[""]") ArrayElement() | ScalarElement() ) <ASSIGN> t=<ID> ) | (
    t=<ID> ) ) <LPAR> {jjtThis.setName(t.image); jjtThis.setToken(t);}
  } catch(ParseException e) {
    error_control(e, "Function", LPAR);
  }
  try {
  ( Varlist() )? <RPAR>
  } catch(ParseException e) {
    error_control(e, "Function", RPAR);
  }
  <LCHAVETA> Stmtlst() <RCHAVETA>
}

void Varlist() : {}
{
  try {
  (LOOKAHEAD(2) ArrayElement() | ScalarElement() ) ( <VIRG> (LOOKAHEAD(2) ArrayElement() | ScalarElement() ))*
  } catch(ParseException e) {

    skipt_do_not_consume_last(e, "Varlist", RPAR);
  }
}

void ArrayElement() : {Token t;}
{

    t=<ID> "[" "]"
    {jjtThis.setName(t.image); jjtThis.setToken(t);}
}

void ScalarElement() : {Token t;}
{

    t=<ID>
    {jjtThis.setName(t.image); jjtThis.setToken(t);}
}

void Stmtlst() : {}
{

    ( Stmt() )*

}

void Stmt() : {}
{
  try {
  While()
  | If()
  | LOOKAHEAD(3) Assign()
  | CallStmt()
  } catch(ParseException e) {
    error_control(e, "Stmt", PVIRG);
  }
}

void Assign() : {}
{

  Lhs() <ASSIGN> Rhs() <PVIRG>

}

void Lhs() : {}
{

  LOOKAHEAD(<ID>"[") ArrayAccess()
  | ScalarAccess()

}

void Rhs() : {Token t;}
{

  ( Term() ( ( t=<ARITH_OP>   {jjtThis.setOp(t.image);}
  | t=<BITWISE_OP>   {jjtThis.setOp(t.image);}
  | t=<ADDSUB_OP>   {jjtThis.setOp(t.image);})
  Term() )? )
  | "[" ArraySize() "]"


}

void ArraySize() : {Token t;}
{

  ScalarAccess()
  | ( t=<INTEGER>  {jjtThis.setName(t.image);} )

}

void Term() : {Token t1, t2;}
{

  ( t1=<ADDSUB_OP> {jjtThis.setOp(t1.image);} )?
  ( t2=<INTEGER> {jjtThis.setName(t2.image);}  |LOOKAHEAD(3) Call() |LOOKAHEAD(<ID>"[") ArrayAccess() |
  ScalarAccess() )

}

void Exprtest() : {Token t;}
{
  try {
  <LPAR> Lhs() t=<RELA_OP> {jjtThis.setOp(t.image); } Rhs() <RPAR>
  } catch(ParseException e) {
    error_control(e, "Exprtest", RPAR);
  }
}

void While() : {}
{
  try {
    <WHILE> Exprtest() <LCHAVETA>
  } catch(ParseException e) {
    error_control(e, "While", LCHAVETA);
  }
  Stmtlst() <RCHAVETA>
}

void If() : {}
{

    <IF> Exprtest() <LCHAVETA>
  Stmtlst() <RCHAVETA>
  ( <ELSE> <LCHAVETA>
  Stmtlst() <RCHAVETA> )?

}

void Call() : {Token t1, t2;}
{
  t1=<ID> {jjtThis.setId1(t1.image); jjtThis.setToken(t1);}
  ( "." t2=<ID> {jjtThis.setId2(t2.image);} )? <LPAR>
  try {
    ( ArgumentList() )? <RPAR>
  } catch(ParseException e) {
    error_control(e, "Call", RPAR);

  }
}

void CallStmt() : {Token t1, t2;}
{

  t1=<ID> {jjtThis.setId1(t1.image); jjtThis.setToken(t1);}
  ( "." t2=<ID> {jjtThis.setId2(t2.image);} )? <LPAR>

  try {
      ( ArgumentList() )? <RPAR>
  } catch(ParseException e) {
    error_control(e, "Call", RPAR);
    Token t = getToken(1);

    if(t.kind == LCHAVETA)
      ErrorBlock();

    else if(t.kind == PVIRG)
      getNextToken();
      return;
  }

  <PVIRG>
}

void ArgumentList() : {}
{

    Argument() ( <VIRG> Argument() )*

}

void Argument() : {Token t1, t2, t3;}
{

  (
      t1=<ID> {jjtThis.setName(t1.image); jjtThis.setToken(t1);}
    | t2=<STRING> {jjtThis.setString(t2.image); jjtThis.setToken(t2);}
    | t3=<INTEGER> {jjtThis.setNumber(t3.image); jjtThis.setToken(t3);}
  )

}

void ArrayAccess() : {Token t;}
{

  t=<ID>{jjtThis.setName(t.image); jjtThis.setToken(t);} "[" Index() "]"

}

void ScalarAccess() : {Token t, size;}
{

  t=<ID> ( "." size=<SIZE> {jjtThis.setSize(size.image);} )?
  {jjtThis.setName(t.image); jjtThis.setToken(t);}
}

void Index() : {Token t1, t2;}
{
  t1=<ID> {jjtThis.setName(t1.image);  jjtThis.setToken(t1);}
  | t2=<INTEGER> {jjtThis.setNumber(t2.image); jjtThis.setToken(t2);}

}

void ErrorBlock() : {}
{
  <LCHAVETA> Stmtlst() <RCHAVETA>
}
