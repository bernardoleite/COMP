options
{
 LOOKAHEAD=1;
 static = false;
}

PARSER_BEGIN(YAL2JVM)
import java.io.* ;
// código Java que invoca o parser
public class YAL2JVM {

public int errors = 0; // use this variable in order to control the maximum of errors (10)

 public static void main(String args[]) throws ParseException {
// criação do objecto utilizando o constructor com argumento para
// ler do standard input (teclado)
 YAL2JVMTokenManager tm;

  if (args.length<1) {
        System.out.println("Ficheiro não especificado!");
        System.exit(1);
  }
  try{
      java.io.InputStream infile;
      infile = new java.io.FileInputStream(args[0]);
      tm = new   YAL2JVMTokenManager(new SimpleCharStream(infile));
      System.out.println("Ficheiro lido com sucesso!");
      YAL2JVM parser = new YAL2JVM(infile);
      parser.Sentence();
      System.out.println("Leitura terminada.");
  }
  catch (java.io.FileNotFoundException e) {
      System.out.println("Ficheiro "+args[0]+" não encontrado");
      return; 
    }

 }

void error_skipto(int kind) {
  ParseException e = generateParseException();  
  System.out.println(e.toString());  
  Token t;
  do {
    t = getNextToken();
  } while (t.kind != kind);
}

void error_control(ParseException e) {

  errors++;

  if(errors > 10)
    System.exit(1);

  System.out.println(e.currentToken.image + " <-- Line:" + e.currentToken.beginLine + ", column:" + e.currentToken.beginColumn);
}

}
PARSER_END(YAL2JVM)


void Sentence():
{}
{
  try{
    Module()
  }catch (ParseException e){
  System.out.println("--->Exception Catched!!! The message: <---");
  error_control(e);
  //error_skipto(PVIRG); //Descomentar para o programar continuar até encontrar Ponto e Virgula
  }
}


// símbolos que não devem ser considerados na análise
SKIP :
{
  " "
  | "\t"
  | "\n"
  | "\r"
  | <"//" (~["\n","\r"])* ("\n" | "\r" | "\r\n")>
  | <"/*" (~["*"])* "*" ("*" | ~["*","/"] (~["*"])* "*")* "/">
}
// definição dos tokens (símbolos terminais)
/* reserved words */
<DEFAULT> TOKEN : {
<RELA_OP: ">" | "<" | "<=" | ">=" | "==" | "!=">
| <ADDSUB_OP: "+" | "-">
| <ARITH_OP: "*" | "/" | "<<" | ">>" | ">>>">
| <BITWISE_OP: "&" | "|" | "^">
| <NOT_OP: "!">
| <WHILE: "while">
| <IF: "if">
| <ELSE: "else">
| <ASSIGN: "=">
| <ASPA: "\"">
| <LPAR: "(">
| <RPAR: ")">
| <VIRG: ",">
| <PVIRG: ";">
| <LCHAVETA: "{">
| <RCHAVETA: "}">
| <FUNCTION: "function">
| <MODULE: "module">
| <SIZE: "size">
}

<DEFAULT> TOKEN : {
<INTEGER: (<DIGIT>)+>
| <ID: <LETTER> (<LETTER> | <DIGIT>)*>
| <#LETTER: ["$","A"-"Z","_","a"-"z"]>
| <#DIGIT: ["0"-"9"]>
| <STRING: "\"" (["a"-"z","A"-"Z","0"-"9",":"," ","="])+ "\"">
}

// definição da produção

void Module() : {}
{
  try{
  <MODULE> <ID> <LCHAVETA>
  (LOOKAHEAD(2) Declaration() )* ( Function() )*
  <RCHAVETA>
  }
  catch(ParseException e){
    System.out.println("yal2jvm.jj:error: Something went wrong on Module");
    error_control(e);
  }

}

void Declaration() : {}
{
  (LOOKAHEAD(<ID>"[""]") ArrayElement() | ScalarElement() ) ( <ASSIGN> ( ( "[" ArraySize() "]" ) | (
  <ADDSUB_OP> )? <INTEGER> ) )? <PVIRG>
}

void Function() : {}
{
  try{
  ( LOOKAHEAD(3)( <FUNCTION> (LOOKAHEAD(<ID>"[""]") ArrayElement() | ScalarElement() ) <ASSIGN> <ID> <LPAR> (
  Varlist() )? <RPAR> ) | ( <FUNCTION> <ID> <LPAR> ( Varlist() )? <RPAR> ) )
  <LCHAVETA> Stmtlst() <RCHAVETA>
  }
  catch(ParseException e){
    System.out.println("yal2jvm.jj:error: Something went wrong on Function");
    error_control(e);
  }

}

void Varlist() : {}
{
  (LOOKAHEAD(2) ArrayElement() | ScalarElement() ) ( <VIRG> (LOOKAHEAD(<ID>"[""]") ArrayElement() | ScalarElement() ))*
}

void ArrayElement() : {}
{
  try{
  <ID> "[" "]"
  }
  catch(ParseException e){
    System.out.println("yal2jvm.jj:error: Something went wrong on ArrayElement");
    error_control(e);
    }
}

void ScalarElement() : {}
{
  <ID>
}

void Stmtlst() : {}
{
  try{
    ( Stmt() )*
    }
  catch(ParseException e){
    System.out.println("yal2jvm.jj:error: Something went wrong on Stmlst");
    error_control(e);
    }
}

void Stmt() : {}
{
  While()
  | If()
  | LOOKAHEAD(3) Assign()
  | Call() <PVIRG>
}

void Assign() : {}
{
  try{
  Lhs() <ASSIGN> Rhs() <PVIRG>
  }
  catch(ParseException e){
    System.out.println("yal2jvm.jj:error: Something went wrong on Assign");
    error_control(e);
    }
}

void Lhs() : {}
{
  LOOKAHEAD(<ID>"[") ArrayAccess()
  | ScalarAccess()
}

void Rhs() : {}
{
  try{
  ( Term() ( ( <ARITH_OP> | <BITWISE_OP> | <ADDSUB_OP> ) Term() )? )
  | "[" ArraySize() "]"
  }
  catch(ParseException e){
    System.out.println("yal2jvm.jj:error: Something went wrong on Rhs");
    error_control(e);
  }
}

void ArraySize() : {}
{
  ScalarAccess()
  | <INTEGER>
}

void Term() : {}
{
  try{
  ( <ADDSUB_OP> )? ( <INTEGER> |LOOKAHEAD(3) Call() |LOOKAHEAD(<ID>"[") ArrayAccess() | ScalarAccess() )
  }
  catch(ParseException e){
    System.out.println("yal2jvm.jj:error: Something went wrong on Term");
    error_control(e);
  }
}

void Exprtest() : {}
{
  <LPAR> Lhs() <RELA_OP> Rhs() <RPAR>
}

void While() : {}
{
  try{
  <WHILE> Exprtest() <LCHAVETA> Stmtlst() <RCHAVETA>
  }
  catch(ParseException e){
    System.out.println("yal2jvm.jj:error: Something went wrong on While");
    error_control(e);
  }
}

void If() : {}
{
  <IF> Exprtest() <LCHAVETA> Stmtlst() <RCHAVETA> ( <ELSE> <LCHAVETA>
  Stmtlst() <RCHAVETA> )?
}

void Call() : {}
{
  try{
  <ID> ( "." <ID> )? <LPAR> ( ArgumentList() )? <RPAR>
  }
  catch(ParseException e){
    System.out.println("yal2jvm.jj:error: Something went wrong on Call");
    error_control(e);
  }
}

void ArgumentList() : {}
{
 Argument() ( <VIRG> Argument() )*
}

void Argument() : {}
{
  try{
  ( <ID> | <STRING> | <INTEGER> )
  }
  catch(ParseException e){
    System.out.println("yal2jvm.jj:error: Something went wrong on Argument");
    error_control(e);
  }
}

void ArrayAccess() : {}
{
  <ID> "[" Index() "]"
}

void ScalarAccess() : {}
{
  try{
  <ID> ( "." <SIZE> )?
  }
  catch(ParseException e){
    System.out.println("yal2jvm.jj:error: Something went wrong on ScalarAccess");
    error_control(e);
  }
}

void Index() : {}
{
  <ID>
  | <INTEGER>
}
